plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.5'
    id 'io.papermc.paperweight.userdev' version '2.0.0-beta.19'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

group = 'org.macl'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
}

dependencies {
    // Paper dev bundle (replaces paper-api and paper-nms)
    paperweight.paperDevBundle("1.21.10-R0.1-SNAPSHOT")

    // HologramLib (provided by server)
    compileOnly("com.github.HologramLib:HologramLib:1.8.3.2") {
        exclude group: "com.tcoded", module: "FoliaLib"
    }

    // HikariCP and MySQL (will be shaded into final JAR)
    implementation "com.zaxxer:HikariCP:5.1.0"
    implementation "com.mysql:mysql-connector-j:8.2.0"

    // Apache Commons IO (for WorldManager file operations)
    implementation "commons-io:commons-io:2.18.0"
}

// Configure paperweight to use Spigot mappings in production
// (change to MOJANG_PRODUCTION if you only want to support Paper 1.20.5+)
paperweight.reobfArtifactConfiguration = io.papermc.paperweight.userdev.ReobfArtifactConfiguration.getMOJANG_PRODUCTION()

tasks {
    runServer {
        minecraftVersion("1.21")
    }

    jar {
        archiveClassifier = 'plain'
    }

    shadowJar {
        // Comment out relocations temporarily
        // relocate 'com.zaxxer.hikari', 'org.macl.ctc.libs.hikari'
        // relocate 'com.mysql', 'org.macl.ctc.libs.mysql'
        // relocate 'org.apache.commons.io', 'org.macl.ctc.libs.commonsio'

        // Exclude signature files
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'

        archiveClassifier = ''
    }

    assemble {
        dependsOn(reobfJar)
    }

    reobfJar {
        mustRunAfter(jar)  // Add this line
    }
}

task copyToServer(type: Copy) {
    dependsOn reobfJar
    from reobfJar.outputJar
    into 'C:/Users/nmacl/OneDrive/Desktop/SERVER23/plugins'

    doLast {
        println "Successfully copied to SERVER23/plugins/"
    }
}

build.dependsOn(copyToServer)


def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}